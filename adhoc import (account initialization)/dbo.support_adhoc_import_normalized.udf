IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[support_adhoc_import_normalized]') AND type in (N'FN', N'IF', N'TF', N'FS', N'FT'))
DROP FUNCTION [dbo].[support_adhoc_import_normalized]
GO

SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER OFF
GO

CREATE FUNCTION [dbo].[support_adhoc_import_normalized] (
)	
RETURNS @table TABLE
(
	  AcctCode nvarchar(20)
	, AcctName nvarchar(50)
	, AcctAddress nvarchar(128)
	, AcctCity nvarchar(50)
	, AcctStateProvince nvarchar(5)
	, AcctPostalCode nvarchar(15)
	, AcctType nvarchar(50)
	, AcctCategory nvarchar(50)
	, MfstCode nvarchar(20)
	, MfstName nvarchar(50)
	, PubCode nvarchar(5)
	, PubName nvarchar(50)
	, DrawDate datetime
	, DrawAmount int
)

AS
/************************************************************************

	Table-Valued-Function that can convert Default Draw (draw by Weekday)
	into a date-driven format that the import process can use

	$History: $

*************************************************************************/
BEGIN
	declare @dayofweek nvarchar(9)
	declare @date datetime
	set @dayofweek = 'Sunday'
		
	select @date = case @dayofweek
			when 'Monday' then dateadd( week, datediff(week,0,getdate()), 7	)
			when 'Tuesday' then dateadd( week, datediff(week,0,getdate()), 8	)
			when 'Wednesday' then dateadd( week, datediff(week,0,getdate()), 9	)
			when 'Thursday' then dateadd( week, datediff(week,0,getdate()), 10	)
			when 'Friday' then dateadd( week, datediff(week,0,getdate()), 11	)
			when 'Satday' then dateadd( week, datediff(week,0,getdate()), 12	)
			when 'Sunday' then dateadd( week, datediff(week,0,getdate()), 13	)
			end


	;with cte
	as (
		select 1 as [DrawWeekday]
			, convert(varchar, @date, 1) as [DrawDate]
			, ld.[AcctCode] as [AcctCode]
			, ld.[AcctName] as [AcctName]
			, ld.[AcctAddress] as [AcctAddress]
			, ld.[AcctCity] as [AcctCity]
			, ld.[AcctStateProvince] as [AcctStateProvince]
			, ld.[AcctPostalCode] as [AcctPostalCode]
			, ld.[AcctType] as [AcctType]
			, ld.[AcctCategory] as [AcctCategory]
			, ld.[MfstCode] as [MfstCode]
			, ld.[MfstName] as [MfstName]
			, ld.[PubCode] as [PubCode]
			, ld.[PubName] as [PubName]
			, ld.[SUN] as DrawAmount
		from support_adhoc_import_Load ld
		union all 
		select [DrawWeekday] + 1
			, convert(varchar, dateadd(d, 1, [DrawDate]), 1) as [DrawDate]
			, ld.[AcctCode] as [AcctCode]
			, ld.[AcctName] as [AcctName]
			, ld.[AcctAddress] as [AcctAddress]
			, ld.[AcctCity] as [AcctCity]
			, ld.[AcctStateProvince] as [AcctStateProvince]
			, ld.[AcctPostalCode] as [AcctPostalCode]
			, ld.[AcctType] as [AcctType]
			, ld.[AcctCategory] as [AcctCategory]
			, ld.[MfstCode] as [MfstCode]
			, ld.[MfstName] as [MfstName]
			, ld.[PubCode] as [PubCode]
			, ld.[PubName] as [PubName]
			, case [DrawWeekday] + 1
				when 2 then [MON]
				when 3 then [TUE]
				when 4 then [WED]
				when 5 then [THU]
				when 6 then [FRI]
				when 7 then [SAT]
				end as [DrawAmount]

		from cte
		join support_adhoc_import_Load ld
			on cte.AcctCode = ld.AcctCode
			and cte.PubCode = ld.PubCode
		where
			[DrawWeekday] + 1 <= 7
		)
	insert into @table ( 
		  AcctCode
		, AcctName
		, AcctAddress
		, AcctCity
		, AcctStateProvince
		, AcctPostalCode
		, AcctType
		, AcctCategory
		, MfstCode
		, MfstName
		, PubCode
		, PubName
		, DrawDate
		, DrawAmount
	)
	select 
		  [AcctCode]
		, [AcctName]
		, [AcctAddress]
		, [AcctCity]
		, [AcctStateProvince]
		, [AcctPostalCode]
		, [AcctType]
		, [AcctCategory]
		, [MfstCode]
		, [MfstName]
		, [PubCode]
		, [PubName]
		, [DrawDate]
		, [DrawAmount]
	from cte

	RETURN
END
GO	